#+ summary: Digital objects counts in the latest HRA release

PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX UBERON: <http://purl.obolibrary.org/obo/UBERON_>
PREFIX HRA: <https://purl.humanatlas.io/collection/hra>

SELECT ?organ ?type (COUNT(*) as ?count)
FROM HRA:
WHERE {
  # CTAnn Crosswalks
  {
    SELECT DISTINCT ?organLabel ?digitalObject ?type
    WHERE {
      GRAPH <https://purl.humanatlas.io/graph/ctann-crosswalks> {
        [] a ccf:AnnotationItem ;
            ccf:organ_id ?organIri ;
            ccf:tool ?digitalObject .
      }

      ?organIri ccf:ccf_pref_label ?organPrefLabel .
      BIND(LCASE(STR(?organPrefLabel)) as ?organLabel)
      BIND("CTAnn Crosswalks" as ?type)
    }
  }
  UNION
  # 2D FTU
  {
    ?digitalObject a ccf:FtuIllustration ;
    ccf:ccf_located_in ?organId .
    BIND(IRI(REPLACE(?organId, 'UBERON:', STR(UBERON:))) as ?organIri)
    ?organIri ccf:ccf_pref_label ?organLabel .
    BIND("2D FTU Illustration" as ?type)
  }
  UNION
  # 3D Reference Organs
  {
    ?digitalObject a ccf:SpatialEntity ;
      ccf:has_reference_organ ?digitalObject ;
      ccf:representation_of ?organIri .
    ?organIri ccf:ccf_pref_label ?organLabel .
    BIND("3D Reference Organ" as ?type)
  }
  UNION
  # OMAPs
  {
    ?digitalObject a ccf:MultiplexedAntibodyBasedImagingExperiment ;
      ccf:sample_organ ?organIri .

    ?organIri ccf:ccf_pref_label ?organLabel .
    BIND("OMAP" as ?type)
  }
  UNION
  # ASCT+B Tables
  {
    HRA: prov:hadMember ?digitalObject .
    BIND(REPLACE(REPLACE(REPLACE(REPLACE(STRBEFORE(
      REPLACE(STR(?digitalObject), 'https://purl.humanatlas.io/asct-b/', ''), '/'), '-', ' '),
      'allen ', ''), ' full term', ''),
      'prostate', 'prostate gland') as ?organLabel)
    FILTER(STRSTARTS(STR(?digitalObject), 'https://purl.humanatlas.io/asct-b/'))

    BIND("ASCT+B Table" as ?type)
  }
  FILTER(!CONTAINS(STR(?digitalObject), 'crosswalk'))

  BIND(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(
      REPLACE(LCASE(?organLabel), 'left ', ''), 'right ', ''),
      'skin of body', 'skin'),
      'male reproductive system', 'prostate gland'),
      'extrapulmonary bronchus', 'main bronchus'),
      'mesenteric lymph node', 'lymph node'),
      'retina', 'eye'),
      'thoracic thymus', 'thymus'),
      'hippocampal gyrus (formation)', 'brain'),
      'respiratory system', 'lung') as ?organ)
}
GROUP BY ?organ ?type
ORDER BY ?organ ?type
