#+ summary: Visualization data for HRApop data mapped to the HRA

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ASCTB-TEMP: <https://purl.org/ccf/ASCTB-TEMP_>
PREFIX CL: <http://purl.obolibrary.org/obo/CL_>
PREFIX FMA: <http://purl.org/sig/ont/fma/fma>
PREFIX UBERON: <http://purl.obolibrary.org/obo/UBERON_>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX HRA: <https://purl.humanatlas.io/collection/hra-api>
PREFIX HRApop: <https://purl.humanatlas.io/graph/hra-pop>
PREFIX HRApopFull: <https://purl.humanatlas.io/ds-graph/hra-pop-full>
PREFIX HRApopTestData: <https://purl.humanatlas.io/graph/hra-pop#test-data>
PREFIX hra-pop: <https://purl.humanatlas.io/graph/hra-pop#>
PREFIX dc: <http://purl.org/dc/terms/>
PREFIX hubmap: <https://entity.api.hubmapconsortium.org/entities/>
PREFIX rui: <http://purl.org/ccf/1.5/>

SELECT ?measure ?group ?group_value (COUNT(DISTINCT ?dataset) as ?count)
FROM HRA:
FROM HRApop:
FROM HRApopFull:
FROM HRApopTestData:
FROM NAMED HRApop:
WHERE {
  {
    SELECT ?dataset ("UNIVERSE" as ?measure)
    WHERE {
      [] ccf:generates_dataset ?dataset .
    }
  }
  UNION
  {
    SELECT ?dataset ("ATLAS" as ?measure)
    WHERE {
      [] ccf:generates_dataset ?dataset .
      FILTER(EXISTS {
        GRAPH HRApop: {
          ?dataset ccf:has_cell_summary [];
        }
      })
    }
  }
  UNION
  {
    SELECT ?dataset ("RUI registered" as ?measure)
    WHERE {
      {
        ?sample ccf:comes_from ?donor .
        ?sample ccf:has_registration_location ?rui_location .
        ?sample ccf:generates_dataset ?dataset .
      } UNION {
        ?block ccf:comes_from ?donor .
        ?block ccf:subdivided_into_sections ?sample .
        ?block ccf:has_registration_location ?rui_location .
        ?sample ccf:generates_dataset ?dataset .
      }
    }
  }
  UNION
  {
    SELECT ?dataset ("CTann via transcriptomics" as ?measure)
    WHERE {
      [] ccf:generates_dataset ?dataset .
      ?dataset ccf:has_cell_summary [
        ccf:modality ?modality ;
      ]
      FILTER(?modality = 'sc_transcriptomics')
    }
  }
  UNION
  {
    SELECT ?dataset ("CTann via proteomics" as ?measure)
    WHERE {
      [] ccf:generates_dataset ?dataset .
      ?dataset ccf:has_cell_summary [
        ccf:modality ?modality ;
      ]
      FILTER(?modality = 'sc_proteomics')
    }
  }

  {
    SELECT ?dataset ("Portal" as ?group) (STR(?portal) as ?group_value)
    WHERE {
      {
        ?sample ccf:comes_from ?donor .
        ?sample ccf:generates_dataset ?dataset .
      } UNION {
        ?block ccf:comes_from ?donor .
        ?block ccf:subdivided_into_sections ?sample .
        ?sample ccf:generates_dataset ?dataset .
      }
      ?donor ccf:consortium_name ?portal .
    }
  }
  UNION
  {
    SELECT ?dataset ("Organ" as ?group) (SAMPLE(?organ) as ?group_value)
    WHERE {
      [] ccf:generates_dataset ?dataset .

      {
        {
          ?sample ccf:comes_from ?donor .
          ?sample ccf:has_registration_location ?rui_location .
          ?sample ccf:generates_dataset ?dataset .
        } UNION {
          ?block ccf:comes_from ?donor .
          ?block ccf:subdivided_into_sections ?sample .
          ?block ccf:has_registration_location ?rui_location .
          ?sample ccf:generates_dataset ?dataset .
        }

        ?placement a ccf:SpatialPlacement ;
          ccf:placement_for ?rui_location ;
          ccf:placement_relative_to ?ref_organ .

        ?ref_organ ccf:representation_of ?refOrganIri .
        ?refOrganIri rdfs:label ?ruiOrganLabel .
      }
      UNION
      {
        ?dataset ccf:organ_id ?_reportedOrganIri .
        BIND(IRI(?_reportedOrganIri) as ?reportedOrganIri)
        ?reportedOrganIri rdfs:label ?reportedOrganLabel .
      }
      BIND(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(
        LCASE(COALESCE(?reportedOrganLabel, ?ruiOrganLabel, "Unknown")), 'left ', ''), 'right ', ''), 'respiratory system', 'lung'), 
        'male reproductive system', 'prostate gland'), 'set of lactiferous glands in breast', 'mammary gland'), 
        'mesenteric lymph node', 'lymph node'), 'blood vasculature', 'vasculature'), 'hippocampal formation', 'brain'), 
        'skin of body', 'skin'), 'islet of langerhans', 'pancreas'), 'primary motor cortex', 'brain'), 
        'vasculature', 'blood vasculature') as ?organ)
    }
    GROUP BY ?dataset
  }
  UNION
  {
    SELECT ?dataset ("Sex" as ?group) (STR(?sex) as ?group_value)
    WHERE {
      {
        ?sample ccf:comes_from ?donor .
        ?sample ccf:generates_dataset ?dataset .
      } UNION {
        ?block ccf:comes_from ?donor .
        ?block ccf:subdivided_into_sections ?sample .
        ?sample ccf:generates_dataset ?dataset .
      }
      ?donor ccf:sex ?sex .
    }
  }
  UNION
  {
    SELECT ?dataset ("Age" as ?group) (STR(?age) as ?group_value)
    WHERE {
      {
        ?sample ccf:comes_from ?donor .
        ?sample ccf:generates_dataset ?dataset .
      } UNION {
        ?block ccf:comes_from ?donor .
        ?block ccf:subdivided_into_sections ?sample .
        ?sample ccf:generates_dataset ?dataset .
      }
      OPTIONAL { ?donor ccf:age ?donor_age . }
      BIND(COALESCE(?donor_age, 'Unknown') as ?age)
    }
  }
  UNION
  {
    SELECT ?dataset ("Ethnicity" as ?group) (REPLACE(STR(?race), 'unknown', 'Unknown') as ?group_value)
    WHERE {
      {
        ?sample ccf:comes_from ?donor .
        ?sample ccf:generates_dataset ?dataset .
      } UNION {
        ?block ccf:comes_from ?donor .
        ?block ccf:subdivided_into_sections ?sample .
        ?sample ccf:generates_dataset ?dataset .
      }
      OPTIONAL { ?donor ccf:race ?donor_race . }
      BIND(COALESCE(?donor_race, 'Unknown') as ?race)
    }
  }
}
GROUP BY ?measure ?group ?group_value
ORDER BY ?measure ?group ?group_value
