#+ summary: get FTU images and parts

PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX UBERON: <http://purl.obolibrary.org/obo/UBERON_>
PREFIX CL: <http://purl.obolibrary.org/obo/CL_>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX HRA: <https://purl.humanatlas.io/collection/hra>

SELECT ?organ ?ftu_label ?ftu_id ?ftu_part_label ?ftu_part_id
  (IF(?ftu_part_type = 'AS', COUNT(DISTINCT ?ftu_part), 0) as ?as_count)
  (IF(?ftu_part_type = 'CT', COUNT(DISTINCT ?ftu_part), 0) as ?ct_count)
FROM HRA:
WHERE {
  [] a ccf:FtuIllustration ;
      ccf:ccf_located_in ?organ_iri ;
      ccf:representation_of ?ftu_iri ;
      ccf:illustration_node ?ftu_part ;
  .
  ?ftu_part a ccf:FtuIllustrationNode ;
    ccf:representation_of ?ftu_part_iri ;
  .

  BIND(IF(STRSTARTS(STR(?ftu_part_iri), STR(UBERON:)), 'AS', 'CT') as ?ftu_part_type)
  ?organ_iri rdfs:label ?organ .
  ?ftu_iri rdfs:label ?ftu_label .
  ?ftu_part_iri rdfs:label ?ftu_part_label .

  BIND(REPLACE(STR(?ftu_iri), STR(UBERON:), "UBERON:") as ?ftu_id)
  BIND(REPLACE(REPLACE(STR(?ftu_part_iri), STR(obo:), ""), "_", ":") as ?ftu_part_id)
}
GROUP BY ?organ ?ftu_label ?ftu_id ?ftu_part_label ?ftu_part_id ?ftu_part_type
ORDER BY ?organ ?ftu_label ?ftu_part_label