#+ summary: Digital objects counts in the latest HRA release

PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX HRA: <https://purl.humanatlas.io/collection/hra>

SELECT DISTINCT ?version (?organ as ?nameOrgan) ?image (CONCAT(?organ, ' icon') as ?imageAlt) ?name (?purl as ?url) ?alt ?threeDimImage
# SELECT DISTINCT (CONCAT('("', ?organ, '" "assets/images/', REPLACE(LCASE(?organ), ' ', '_'), '.svg")') as ?foo)
FROM <https://lod.humanatlas.io>
WHERE {
  {
    [] a dcat:Dataset ;
      rdfs:seeAlso ?purl ;
      schema:name ?doName ;
      schema:additionalType ?type ;
      prov:wasDerivedFrom [
        schema:dateCreated ?creation_date ;
        dcterms:title ?title ;
      ] .

    # BIND(STRAFTER(STRBEFORE(REPLACE(REPLACE(STR(?title), "Functional Tissue Unit \\(FTU\\) ", ""), " FTU", ""), " for "), "2D ") as ?name)
    # BIND(STRAFTER(STR(?title), " for ") as ?organ)

    BIND(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(STR(?title), "3D Reference Organ for ", ""), "3D Reference organ for ", ""), "3D Reference Organ Set for ", ""), " Full Term,", ""), " \\(mammary gland\\)", ""), " left", ", Left"), " right", ", Right"), "United", "All Organs"), "Breast", "Mammary Gland") as ?fixed)

    BIND(STRBEFORE(?fixed, ",") as ?organ)
    BIND(REPLACE(REPLACE(STRAFTER(STR(?fixed), ", "), ", Left", ", L"), ", Right", ", R") as ?name)
    BIND(?fixed as ?alt)

    FILTER(?type = 'ref-organ' && ?doName != 'asct-b-3d-models-crosswalk')
  }

  {
    [] a dcat:Dataset ;
      rdfs:seeAlso ?purl ;
      prov:wasDerivedFrom [
        dcat:distribution [
          dcat:downloadURL ?glbDownload ;
          dcat:mediaType ?glbMediaType ;
        ] ;
      ] .

    BIND(?glbDownload as ?threeDimImage)
    FILTER(?glbMediaType = 'model/gltf-binary')
  }

  {
    SELECT ?release ?version ?purl
    WHERE {
      [] a dcat:Dataset ;
        rdfs:seeAlso ?release ;
        schema:name ?releaseName ;
        schema:additionalType ?releaseType ;
        schema:version ?versionStr ;
        prov:hadMember ?purlStr ;
      .

      BIND(REPLACE(?versionStr, 'v', '') as ?version)
      BIND(URI(STR(?purlStr)) as ?purl)
      # FILTER(?releaseType = 'collection' && ?releaseName = 'hra' && ?versionStr = 'v2.0')
    }
  }

  OPTIONAL {
    VALUES (?organ ?image) {
      ("Blood Vasculature" "assets/images/blood_vasculature.svg")
      ("Brain" "assets/images/brain.svg")
      ("Mammary Gland" "assets/images/mammary_gland.svg")
      ("Eye" "assets/images/eye.svg")
      ("Fallopian Tube" "assets/images/fallopian_tube.svg")
      ("Heart" "assets/images/heart.svg")
      ("Kidney" "assets/images/kidney.svg")
      ("Knee" "assets/images/knee.svg")
      ("Large Intestine" "assets/images/large_intestine.svg")
      ("Intestine" "assets/images/large_intestine.svg")
      ("Larynx" "assets/images/larynx.svg")
      ("Liver" "assets/images/liver.svg")
      ("Lung" "assets/images/lung.svg")
      ("Lymph Node" "assets/images/lymph_node.svg")
      ("Main Bronchus" "assets/images/extrapulmonaryBronchus.svg")
      ("Ovary" "assets/images/ovary.svg")
      ("Palatine Tonsil" "assets/images/palatineTonsil.svg")
      ("Pancreas" "assets/images/pancreas.svg")
      ("Pelvis" "assets/images/bone_marrow.svg")
      ("Placenta" "assets/images/placenta.svg")
      ("Prostate" "assets/images/prostate.svg")
      ("Skin" "assets/images/skin.svg")
      ("Small Intestine" "assets/images/small_intestine.svg")
      ("Spinal Cord" "assets/images/spinal_cord.svg")
      ("Spleen" "assets/images/spleen.svg")
      ("Thymus" "assets/images/thymus.svg")
      ("Trachea" "assets/images/trachea.svg")
      ("All Organs" "assets/images/all_organs.svg")
      ("Ureter" "assets/images/ureter.svg")
      ("Urinary Bladder" "assets/images/urinary_bladder.svg")
      ("Uterus" "assets/images/uterus.svg")
    }
  }
}
ORDER BY ?version ?organ ?name
