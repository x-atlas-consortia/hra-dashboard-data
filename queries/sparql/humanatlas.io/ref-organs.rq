#+ summary: Digital objects counts in the latest HRA release

PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX HRA: <https://purl.humanatlas.io/collection/hra>

SELECT DISTINCT ?version (?organ as ?nameOrgan) ?image (CONCAT(?organ, ' icon') as ?imageAlt) ?name (?purl as ?url) ?alt ?threeDimImage ?csv
# SELECT DISTINCT (CONCAT('("', ?organ, '" "assets/images/', REPLACE(LCASE(?organ), ' ', '_'), '.svg")') as ?foo)
FROM <https://lod.humanatlas.io>
WHERE {
  {
    [] a dcat:Dataset ;
      rdfs:seeAlso ?purl ;
      schema:name ?doName ;
      schema:additionalType ?type ;
      prov:wasDerivedFrom [
        schema:dateCreated ?creation_date ;
        dcterms:title ?title ;
      ] .

    # BIND(STRAFTER(STRBEFORE(REPLACE(REPLACE(STR(?title), "Functional Tissue Unit \\(FTU\\) ", ""), " FTU", ""), " for "), "2D ") as ?name)
    # BIND(STRAFTER(STR(?title), " for ") as ?organ)

    BIND(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(REPLACE(STR(?title), "3D Reference Organ for ", ""), "3D Reference organ for ", ""), "3D Reference Organ Set for ", ""), "3D reference organ for ", ""), " Full Term,", ""), " \\(mammary gland\\)", ""), " left", ", Left"), " right", ", Right"), "United", "All Organs"), "Breast", "Mammary Gland") as ?fixed)

    BIND(STRBEFORE(?fixed, ",") as ?organ)
    BIND(REPLACE(REPLACE(STRAFTER(STR(?fixed), ", "), ", Left", ", L"), ", Right", ", R") as ?name)
    BIND(?fixed as ?alt)

    FILTER(?type = 'ref-organ' && ?doName != 'asct-b-3d-models-crosswalk')
  }

  {
    [] a dcat:Dataset ;
      rdfs:seeAlso ?purl ;
      prov:wasDerivedFrom [
        dcat:distribution [
          dcat:downloadURL ?glbDownload ;
          dcat:mediaType ?glbMediaType ;
        ] ;
      ] .

    BIND(?glbDownload as ?threeDimImage)
    FILTER(?glbMediaType = 'model/gltf-binary')
  }

  {
    [] a dcat:Dataset ;
      rdfs:seeAlso ?purl ;
      prov:wasDerivedFrom [
        dcat:distribution [
          dcat:downloadURL ?csvDownload ;
          dcat:mediaType ?csvMediaType ;
        ] ;
      ] .

    BIND(?csvDownload as ?csv)
    FILTER(?csvMediaType = 'text/csv')
  }

  {
    SELECT ?release ?version ?purl
    WHERE {
      [] a dcat:Dataset ;
        rdfs:seeAlso ?release ;
        schema:name ?releaseName ;
        schema:additionalType ?releaseType ;
        schema:version ?versionStr ;
        prov:hadMember ?purlStr ;
      .

      BIND(REPLACE(?versionStr, 'v', '') as ?version)
      BIND(URI(STR(?purlStr)) as ?purl)
      # FILTER(?releaseType = 'collection' && ?releaseName = 'hra' && ?versionStr = 'v2.0')
    }
  }

  OPTIONAL {
    VALUES (?organ ?image) {
      ("subcutaneous adipose tissue" "organ:adipose")
      ("Blood Vasculature" "organ:blood-vasculature")
      ("Brain" "organ:brain")
      ("Mammary Gland" "organ:mammary-gland")
      ("Eye" "organ:eye")
      ("Fallopian Tube" "organ:fallopian-tube")
      ("Heart" "organ:heart")
      ("Kidney" "organ:kidneys")
      ("Knee" "organ:knee")
      ("Large Intestine" "organ:large-intestine")
      ("intervertebral discs" "organ:intervertebral-discs")
      ("Intestine" "organ:large-intestine")
      ("Larynx" "organ:larynx")
      ("Liver" "organ:liver")
      ("Lung" "organ:lungs")
      ("Lymph Node" "organ:lymph-node")
      ("Main Bronchus" "organ:extrapulmonaryBronchus")
      ("Manubrium" "organ:manubrium")
      ("Mouth" "organ:mouth")
      ("Ovary" "organ:ovary")
      ("Palatine Tonsil" "organ:palatineTonsil")
      ("Pancreas" "organ:pancreas")
      ("Pelvis" "organ:bone-marrow")
      ("Renal Pelvis" "organ:bone-marrow")
      ("Placenta" "organ:placenta")
      ("Prostate" "organ:prostate")
      ("Skin" "organ:skin")
      ("Small Intestine" "organ:small-intestine")
      ("Spinal Cord" "organ:spinal-cord")
      ("Spleen" "organ:spleen")
      ("Sternum" "organ:sternum")
      ("Thymus" "organ:thymus")
      ("Trachea" "organ:trachea")
      ("All Organs" "organ:all-organs")
      ("Ureter" "organ:ureter")
      ("Urinary Bladder" "organ:urinary-bladder")
      ("Uterus" "organ:uterus")
    }
  }
}
ORDER BY ?version ?organ ?name
