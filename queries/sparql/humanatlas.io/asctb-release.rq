PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX HRA: <https://purl.humanatlas.io/collection/hra>
PREFIX ASCTB-TEMP: <https://purl.org/ccf/ASCTB-TEMP_>
PREFIX has_characterizing_biomarker_set: <http://purl.obolibrary.org/obo/RO_0015004>

SELECT ?Organ ?csv ?table_version
  (COALESCE(?AS3D, 0) as ?AS3D)
  (COALESCE(?AS, 0) as ?AS)
  (COALESCE(?CT, 0) as ?CT)
  (COALESCE(?B_Total, 0) as ?B_Total)
  (COALESCE(?BG, 0) as ?BG)
  (COALESCE(?BP, 0) as ?BP)
  (COALESCE(?BM, 0) as ?BM)
  (COALESCE(?BF, 0) as ?BF)
  (COALESCE(?BL, 0) as ?BL)
  (COALESCE(?ASWithNoLink, 0) as ?ASWithNoLink)
  (COALESCE(?CTWithNoLink, 0) as ?CTWithNoLink)
  (COALESCE(?BWithNoLink, 0) as ?BWithNoLink)
  (COALESCE(?ASWithNoCT, 0) as ?ASWithNoCT)
  (COALESCE(?CTWithNoB, 0) as ?CTWithNoB)
  (COALESCE(?AS_AS, 0) as ?AS_AS)
  (COALESCE(?AS_CT, 0) as ?AS_CT)
  (COALESCE(?CT_BM, 0) as ?CT_BM)
  (?graphAndVersion as ?url)
  ("CSV" as ?csvLabel)

WITH {
  SELECT DISTINCT ?purl ?term ?biomarker
  WHERE {    
    GRAPH ?purl {
      ?graph a owl:Ontology .
      FILTER(STRSTARTS(STR(?graph), 'https://purl.humanatlas.io/asct-b/'))
      [] rdf:type ccf:CellMarkerDescriptor ;
                ccf:primary_cell_type ?term ;
                ccf:biomarker ?biomarker .
    }
    hint:Query hint:analytic "true" .
  }
} AS %ct_bm

WHERE {
  hint:Query hint:analytic "true" .

  GRAPH HRA: {
    HRA: prov:hadMember ?graphAndVersion .
  }
  GRAPH <https://lod.humanatlas.io> {
    [] a dcat:Dataset ;
      schema:name ?name ;
      schema:additionalType ?type ;
      schema:version ?table_version ;
      rdfs:seeAlso ?graphAndVersion ;
      prov:wasDerivedFrom ?rawData ;
    .
    FILTER(?type = 'asct-b' && !CONTAINS(?name, '-crosswalk'))
    BIND(IRI(CONCAT('https://purl.humanatlas.io/asct-b/', ?name)) as ?purl)
    BIND(REPLACE(?name, '-', ' ') as ?Organ)

    ?rawData dcat:distribution [
      dcat:downloadURL ?csv ;
      dcat:mediaType ?mediaType ;
    ]  .
    FILTER(?mediaType = 'text/csv')
  }

  OPTIONAL {
    SELECT ?purl (COUNT(DISTINCT(?term)) as ?AS3D)
    WHERE {
      GRAPH ?purl {
        ?graph a owl:Ontology .
        FILTER(STRSTARTS(STR(?graph), 'https://purl.humanatlas.io/asct-b/'))

        ?term ccf:ccf_asctb_type ?asctb_type .
        FILTER(?asctb_type = 'AS')
        FILTER(EXISTS {
          GRAPH HRA: {
            ?refOrgan a ccf:SpatialEntity ; ccf:representation_of ?term .
          }
        })
      }
    }
    GROUP BY ?purl
  }

  OPTIONAL {
    SELECT ?purl (COUNT(DISTINCT(?term)) as ?AS)
    WHERE {
      GRAPH ?purl {
        ?graph a owl:Ontology .
        FILTER(STRSTARTS(STR(?graph), 'https://purl.humanatlas.io/asct-b/'))

        ?term ccf:ccf_asctb_type ?asctb_type .
        FILTER(?asctb_type = 'AS')
      }
    }
    GROUP BY ?purl
  }

  OPTIONAL {
    SELECT ?purl (COUNT(DISTINCT(?term)) as ?CT)
    WHERE {
      GRAPH ?purl {
        ?graph a owl:Ontology .
        FILTER(STRSTARTS(STR(?graph), 'https://purl.humanatlas.io/asct-b/'))

        ?term ccf:ccf_asctb_type ?asctb_type .
        FILTER(?asctb_type = 'CT')
      }
    }
    GROUP BY ?purl
  }

  OPTIONAL {
    SELECT ?purl (COUNT(DISTINCT(?term)) as ?B_Total)
    WHERE {
      GRAPH ?purl {
        ?graph a owl:Ontology .
        FILTER(STRSTARTS(STR(?graph), 'https://purl.humanatlas.io/asct-b/'))

        ?term ccf:ccf_asctb_type ?asctb_type .
        FILTER(?asctb_type = 'BM')
      }
    }
    GROUP BY ?purl
  }

  OPTIONAL {
    SELECT ?purl (COUNT(DISTINCT(?term)) as ?BG)
    WHERE {
      GRAPH ?purl {
        ?graph a owl:Ontology .
        FILTER(STRSTARTS(STR(?graph), 'https://purl.humanatlas.io/asct-b/'))

        ?term ccf:ccf_asctb_type ?asctb_type ;
        ccf:ccf_biomarker_type ?bm_type .
        FILTER(?asctb_type = 'BM' && ?bm_type = 'gene')
      }
    }
    GROUP BY ?purl
  }

  OPTIONAL {
    SELECT ?purl (COUNT(DISTINCT(?term)) as ?BP)
    WHERE {
      GRAPH ?purl {
        ?graph a owl:Ontology .
        FILTER(STRSTARTS(STR(?graph), 'https://purl.humanatlas.io/asct-b/'))

        ?term ccf:ccf_asctb_type ?asctb_type ;
        ccf:ccf_biomarker_type ?bm_type .
        FILTER(?asctb_type = 'BM' && ?bm_type = 'protein')
      }
    }
    GROUP BY ?purl
  }

  OPTIONAL {
    SELECT ?purl (COUNT(DISTINCT(?term)) as ?BM)
    WHERE {
      GRAPH ?purl {
        ?graph a owl:Ontology .
        FILTER(STRSTARTS(STR(?graph), 'https://purl.humanatlas.io/asct-b/'))

        ?term ccf:ccf_asctb_type ?asctb_type ;
        ccf:ccf_biomarker_type ?bm_type .
        FILTER(?asctb_type = 'BM' && ?bm_type = 'metabolites')
      }
    }
    GROUP BY ?purl
  }

  OPTIONAL {
    SELECT ?purl (COUNT(DISTINCT(?term)) as ?BF)
    WHERE {
      GRAPH ?purl {
        ?graph a owl:Ontology .
        FILTER(STRSTARTS(STR(?graph), 'https://purl.humanatlas.io/asct-b/'))

        ?term ccf:ccf_asctb_type ?asctb_type ;
        ccf:ccf_biomarker_type ?bm_type .
        FILTER(?asctb_type = 'BM' && ?bm_type = 'proteoforms')
      }
    }
    GROUP BY ?purl
  }

  OPTIONAL {
    SELECT ?purl (COUNT(DISTINCT(?term)) as ?BL)
    WHERE {
      GRAPH ?purl {
        ?graph a owl:Ontology .
        FILTER(STRSTARTS(STR(?graph), 'https://purl.humanatlas.io/asct-b/'))

        ?term ccf:ccf_asctb_type ?asctb_type ;
        ccf:ccf_biomarker_type ?bm_type .
        FILTER(?asctb_type = 'BM' && ?bm_type = 'lipids')
      }
    }
    GROUP BY ?purl
  }

  OPTIONAL {
    SELECT ?purl (COUNT(DISTINCT(?term)) as ?ASWithNoLink)
    WHERE {
      GRAPH ?purl {
        ?graph a owl:Ontology .
        FILTER(STRSTARTS(STR(?graph), 'https://purl.humanatlas.io/asct-b/'))

        ?term ccf:ccf_asctb_type ?asctb_type .
        FILTER(?asctb_type = 'AS' && STRSTARTS(STR(?term), STR(ASCTB-TEMP:)))
      }
    }
    GROUP BY ?purl
  }

  OPTIONAL {
    SELECT ?purl (COUNT(DISTINCT(?term)) as ?CTWithNoLink)
    WHERE {
      GRAPH ?purl {
        ?graph a owl:Ontology .
        FILTER(STRSTARTS(STR(?graph), 'https://purl.humanatlas.io/asct-b/'))

        ?term ccf:ccf_asctb_type ?asctb_type .
        FILTER(?asctb_type = 'CT' && STRSTARTS(STR(?term), STR(ASCTB-TEMP:)))
      }
    }
    GROUP BY ?purl
  }

  OPTIONAL {
    SELECT ?purl (COUNT(DISTINCT(?term)) as ?BWithNoLink)
    WHERE {
      GRAPH ?purl {
        ?graph a owl:Ontology .
        FILTER(STRSTARTS(STR(?graph), 'https://purl.humanatlas.io/asct-b/'))

        ?term ccf:ccf_asctb_type ?asctb_type .
        FILTER(?asctb_type = 'BM' && STRSTARTS(STR(?term), STR(ASCTB-TEMP:)))
      }
    }
    GROUP BY ?purl
  }

  OPTIONAL {
    SELECT ?purl (COUNT(DISTINCT(?term)) as ?ASWithNoCT)
    WHERE {
      GRAPH ?purl {
        ?graph a owl:Ontology .
        FILTER(STRSTARTS(STR(?graph), 'https://purl.humanatlas.io/asct-b/'))

        ?term ccf:ccf_asctb_type ?asctb_type .
        FILTER(?asctb_type = 'AS')

        GRAPH ?purl {
          SELECT ?term (COUNT(DISTINCT(?ct)) as ?links)
          WHERE {
            ?term ccf:ccf_asctb_type ?asctb_type .
            FILTER(?asctb_type = 'AS')
            OPTIONAL {
              ?ct ccf:ccf_located_in ?term .
            }
          }
          GROUP BY ?term
          HAVING(?links = 0)
        }
      }
    }
    GROUP BY ?purl
  }

  OPTIONAL {
    SELECT ?purl (COUNT(DISTINCT(?term)) as ?CTWithNoB)
    WHERE {
      GRAPH ?purl {
        ?graph a owl:Ontology .
        FILTER(STRSTARTS(STR(?graph), 'https://purl.humanatlas.io/asct-b/'))

        ?term ccf:ccf_asctb_type ?asctb_type .
        FILTER(?asctb_type = 'CT')

        FILTER NOT EXISTS {
          INCLUDE %ct_bm
        }
      }
      FILTER(CONTAINS(STR(?purl), '/asct-b/') && !CONTAINS(STR(?purl), '-crosswalk'))
    }
    GROUP BY ?purl
  }

  OPTIONAL {
    SELECT ?purl (COUNT(*) as ?AS_AS)
    WHERE {
      {
        SELECT DISTINCT ?purl ?as1 ?as2
        WHERE {
          GRAPH ?purl {
            ?graph a owl:Ontology .
            FILTER(STRSTARTS(STR(?graph), 'https://purl.humanatlas.io/asct-b/'))

            ?as1 ccf:ccf_part_of ?as2 .
          }
        }
      }
    }
    GROUP BY ?purl
  }

  OPTIONAL {
    SELECT ?purl (COUNT(*) as ?AS_CT)
    WHERE {
      {
        SELECT DISTINCT ?purl ?as ?ct
        WHERE {
          GRAPH ?purl {
            ?graph a owl:Ontology .
            FILTER(STRSTARTS(STR(?graph), 'https://purl.humanatlas.io/asct-b/'))

            ?ct ccf:ccf_located_in ?as .
          }
        }
      }
    }
    GROUP BY ?purl
  }

  OPTIONAL {
    SELECT ?purl (COUNT(*) as ?CT_BM)
    WHERE {
      INCLUDE %ct_bm
    }
    GROUP BY ?purl
  }
}
ORDER BY ?purl
