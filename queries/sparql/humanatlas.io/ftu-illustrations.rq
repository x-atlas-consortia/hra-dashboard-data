#+ summary: Digital objects counts in the latest HRA release

PREFIX dcat: <http://www.w3.org/ns/dcat#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX prov: <http://www.w3.org/ns/prov#>
PREFIX schema: <http://schema.org/>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX HRA: <https://purl.humanatlas.io/collection/hra>

SELECT DISTINCT ?version (?organ as ?nameOrgan) ?image (CONCAT(?organ, ' icon') as ?imageAlt) ?name (CONCAT('Image of ',?name) as ?alt) (?purl as ?url) ?svg ?ai ?png
FROM <https://lod.humanatlas.io>
WHERE {
  {
    [] a dcat:Dataset ;
        schema:name ?doName ;
        schema:additionalType ?type ;
        rdfs:seeAlso ?purl ;
        prov:wasDerivedFrom [
          schema:dateCreated ?creation_date ;
          dcterms:title ?title ;
        ] .

    BIND(STRAFTER(STRBEFORE(REPLACE(REPLACE(STR(?title), "Functional Tissue Unit \\(FTU\\) ", ""), " FTU", ""), " for "), "2D ") as ?name)
    BIND(STRAFTER(STR(?title), " for ") as ?organ)

    FILTER(?type = '2d-ftu' && ?doName != 'asct-b-2d-models-crosswalk')
  }

  {
    [] a dcat:Dataset ;
      rdfs:seeAlso ?purl ;
      prov:wasDerivedFrom [
        dcat:distribution [
          dcat:downloadURL ?svgDownload ;
          dcat:mediaType ?svgMediaType ;
        ] ;
      ] .

    BIND(?svgDownload as ?svg)
    FILTER(?svgMediaType = 'image/svg+xml')
  }

  OPTIONAL {
    [] a dcat:Dataset ;
      rdfs:seeAlso ?purl ;
      prov:wasDerivedFrom [
        dcat:distribution [
          dcat:downloadURL ?aiDownload ;
          dcat:mediaType ?aiMediaType ;
        ] ;
      ] .

    BIND(?aiDownload as ?ai)
    FILTER(?aiMediaType = 'application/postscript')
  }

  {
    [] a dcat:Dataset ;
      rdfs:seeAlso ?purl ;
      prov:wasDerivedFrom [
        dcat:distribution [
          dcat:downloadURL ?pngDownload ;
          dcat:mediaType ?pngMediaType ;
        ] ;
      ] .

    BIND(REPLACE(STR(?pngDownload), '.svg', '.png') as ?png)
    FILTER(?pngMediaType = 'image/svg+xml')
    #FILTER(?pngMediaType = 'image/png')
  }

  {
    SELECT ?release ?version ?purl
    WHERE {
      [] a dcat:Dataset ;
        rdfs:seeAlso ?release ;
        schema:name ?releaseName ;
        schema:additionalType ?releaseType ;
        schema:version ?versionStr ;
        prov:hadMember ?purlStr ;
      .

      BIND(REPLACE(?versionStr, 'v', '') as ?version)
      BIND(URI(STR(?purlStr)) as ?purl)
      # FILTER(?releaseType = 'collection' && ?releaseName = 'hra' && ?versionStr = 'v2.0')
    }
  }

  OPTIONAL {
    VALUES (?organ ?image) {
      ("Blood Vasculature" "organ:blood-vasculature")
      ("Brain" "organ:brain")
      ("Mammary Gland" "organ:mammary-gland")
      ("Eye" "organ:eye")
      ("Fallopian Tube" "organ:fallopian-tube")
      ("Heart" "organ:heart")
      ("Kidney" "organ:kidneys")
      ("Knee" "organ:knee")
      ("Large Intestine" "organ:large-intestine")
      ("Intestine" "organ:large-intestine")
      ("Larynx" "organ:larynx")
      ("Liver" "organ:liver")
      ("Lung" "organ:lungs")
      ("Lymph Node" "organ:lymph-node")
      ("Main Bronchus" "organ:extrapulmonaryBronchus")
      ("Mouth" "organ:mouth")
      ("Ovary" "organ:ovary")
      ("Palatine Tonsil" "organ:palatineTonsil")
      ("Pancreas" "organ:pancreas")
      ("Pelvis" "organ:bone-marrow")
      ("Placenta" "organ:placenta")
      ("Prostate" "organ:prostate")
      ("Skin" "organ:skin")
      ("Small Intestine" "organ:small-intestine")
      ("Spinal Cord" "organ:spinal-cord")
      ("Spleen" "organ:spleen")
      ("Thymus" "organ:thymus")
      ("Trachea" "organ:trachea")
      ("All Organs" "organ:all-organs")
      ("Ureter" "organ:ureter")
      ("Urinary Bladder" "organ:urinary-bladder")
      ("Uterus" "organ:uterus")
    }
  }
}
ORDER BY ?version ?organ ?name
