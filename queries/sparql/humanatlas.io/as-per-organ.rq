#+ summary: AS per Ref. Organ
#+ description: Number of Anatomical Structures per Reference Organ

PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX ccf: <http://purl.org/ccf/>
PREFIX UBERON: <http://purl.obolibrary.org/obo/UBERON_>
PREFIX FMA: <http://purl.org/sig/ont/fma/fma>
PREFIX HRA: <https://purl.humanatlas.io/collection/hra>

SELECT
  ?organ
  ?sex
  (COUNT(?structure) AS ?as_count)
  (SAMPLE(?uberon_id) as ?uberon_id)
  (REPLACE(STR(?refOrgan), '#primary', '') as ?url)
FROM HRA:
WHERE {
  ?structure ccf:has_reference_organ ?refOrgan .
  ?refOrgan ccf:representation_of ?organIri ;
    ccf:organ_owner_sex ?sex .
  
  OPTIONAL {
    ?organIri ccf:ccf_pref_label ?organ_pref_label .
  }
  OPTIONAL {
    ?organIri rdfs:label ?organ_rdfs_label .
  }

  BIND(IF(BOUND(?organ_pref_label), ?organ_pref_label, ?organ_rdfs_label) as ?organ_label)
  BIND(REPLACE(LCASE(?organ_label), "skin of body", "skin") as ?organ)
  BIND(REPLACE(REPLACE(STR(?organIri), STR(UBERON:), 'UBERON:'), STR(FMA:), 'FMA:') as ?uberon_id)
}
GROUP BY ?refOrgan ?organ ?sex
ORDER BY ?organ ?sex
